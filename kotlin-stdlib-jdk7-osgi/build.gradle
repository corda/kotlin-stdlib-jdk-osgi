plugins {
    id 'com.jfrog.artifactory'
    id 'biz.aQute.bnd.builder'
    id 'maven-publish'
    id 'java-library'
}

description 'The Kotlin Stdlib JDK7 extension as an OSGi bundle.'

configurations {
    osgi {
        extendsFrom runtimeClasspath
        canBeResolved = false
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-osgi-bundle:$kotlin_version"
    api "org.jetbrains:annotations:$jetbrains_annotations_version"
    bundle "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def jar = tasks.named('jar', Jar) {
    archiveVersion = kotlin_version
    archiveBaseName = 'kotlin-stdlib-jdk7-osgi'

    bundle {
        bnd kotlinProvider

        bnd '''\
Bundle-SymbolicName: net.corda.kotlin-stdlib-jdk7.osgi-bundle
Bundle-Name: Kotlin Standard Library JDK7 classes
Import-Package: \
    kotlin.*jdk7:o;provide:=true,\
    kotlin.io.path.*:o;provide:=true,\
    kotlin.*;version='${range;[===,==+);${@}}',\
    android.os.*:o;resolution:=optional,\
    *
'''
    }
}

artifacts {
    osgi jar
}
